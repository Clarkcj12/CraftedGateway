plugins {
    id 'java'
}

group = 'net.sanctuary-servers'
version = '0.1.0-SNAPSHOT'
description = 'CraftedGateway plugin for SanctuaryMC.'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    shade {
        canBeResolved = true
        canBeConsumed = false
    }
    implementation.extendsFrom(shade)
}

repositories {
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url = 'https://repo.aikar.co/content/groups/aikar/'
    }
    mavenCentral()
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'
    implementation 'net.kyori:adventure-api:4.25.0'
    shade 'co.aikar:acf-bukkit:0.5.1-SNAPSHOT'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(17)
}

processResources {
    filesMatching('plugin.yml') {
        expand(version: project.version)
    }
}

tasks.jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from({
        configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }
    })
}
